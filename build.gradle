plugins {
    id 'edu.sc.seis.launch4j' version '2.3.0'
    id 'com.palantir.git-version' version '0.5.3'
}


apply plugin: 'java'
apply plugin: 'application'

group 'net.kurikagononaka'
version gitVersion()

sourceCompatibility = 1.8

mainClassName = "net.kurikagononaka.mapImager.Main"
def executableBaseName = "mapImager"

repositories {
    mavenCentral()

    maven {
        url 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group: 'com.flowpowered', name: 'flow-nbt', version: '1.0.1-SNAPSHOT'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'com.google.guava', name: 'guava', version: '21.0'
}

task executableJar(type: Jar, dependsOn: jar) {
    archiveName = "${executableBaseName}.jar"
    manifest {
        attributes 'Main-Class': mainClassName
    }

    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    from "$buildDir/classes/main"
    from "$buildDir/resources/main"
}


createExe {
    mainClassName = project.mainClassName
    outputDir = "release/mapImager"
    libraryDir = ""
    headerType = "console"
    chdir = '.'
    icon = project.projectDir.toString() + '/icons/map_icon.ico'
}

task releaseCopy(type: Copy, dependsOn: executableJar) {
    from 'build/libs'
    into 'build/release/mapImager'
    include 'mapImager.jar'
}

task release(type: Zip, dependsOn: [clean, createExe, releaseCopy]) {
    from 'build/release/mapImager'
    into ''
}
